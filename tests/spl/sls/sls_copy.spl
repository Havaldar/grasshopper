struct Node {
  var next: Node;
  var data: int;
}

predicate uslseg(x: Node, y: Node, lb: int) {
  x == y || x != y &*& acc(x) &*& lb <= x.data &*& uslseg(x.next, y, x.data)
}

predicate slseg(x: Node, y: Node) {
  x == y || x != y &*& acc(x) &*& uslseg(x.next, y, x.data)
}

predicate llseg(x: Node, y: Node, ub: int) {
  x == y || x != y &*& acc(x) &*& x.data <= ub &*& llseg(x.next, y, ub)
}

predicate lslseg(x: Node, y: Node, ub: int) {
  slseg(x, y) && llseg(x, y, ub)
}

procedure copy(lst: Node) returns (res: Node)
  requires slseg(lst, null);
  ensures slseg(lst, null) &*& slseg(res, null);
{
  if (lst == null) {
    return null;
  } else {
    var curr: Node, cp: Node;
    curr := lst;
    res := new Node;
    cp := res;
    cp.data := curr.data;
    cp.next := null;
    while (curr.next != null) 
      invariant lslseg(lst, curr, curr.data);
      invariant slseg(curr, null);
      invariant lslseg(res, cp, curr.data);
      invariant cp.next |-> null;
      invariant cp.data == curr.data;
      invariant curr != null;
    {
      var old_cp: Node;
      old_cp := cp;
      cp := new Node;
      old_cp.next := cp;
      curr := curr.next;
      cp.data := curr.data;
      cp.next := null;
    }
    //cp.next := null;
    return res;
  }
}
