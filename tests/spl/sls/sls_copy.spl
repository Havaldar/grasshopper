include "../include/slslist.spl";

procedure copy(lst: Node) returns (res: Node)
  requires slseg(lst, null)
  ensures slseg(lst, null) &*& slseg(res, null)
{
  if (lst == null) {
    return null;
  } else {
    res := new Node;
    var curr := lst;
    var cp := res;
    cp.data := curr.data;
    cp.next := null;
    while (curr.next != null) 
      invariant lslseg(lst, curr, curr.data)
      invariant slseg(curr, null)
      invariant lslseg(res, cp, curr.data)
      invariant cp.next |-> null
      invariant cp.data == curr.data
      invariant curr != null
    {
      var old_cp: Node;
      old_cp := cp;
      cp := new Node;
      old_cp.next := cp;
      curr := curr.next;
      cp.data := curr.data;
      cp.next := null;
    }
    return res;
  }
}
