struct Node {
  var next: Node;
  var data: Int;
}

predicate uslseg(x: Node, y: Node, lb: Int) {
  x == y || x != y &*& acc(x) &*& lb <= x.data &*& uslseg(x.next, y, x.data)
}

predicate slseg(x: Node, y: Node) {
  x == y || x != y &*& acc(x) &*& uslseg(x.next, y, x.data)
}

predicate llseg(x: Node, y: Node, ub: Int) {
  x == y || x != y &*& acc(x) &*& x.data <= ub &*& llseg(x.next, y, ub)
}

predicate lslseg(x: Node, y: Node, ub: Int) {
  slseg(x, y) && llseg(x, y, ub)
}

procedure remove(lst: Node, value: Int) returns (res: Node)
  requires slseg(lst, null);
  ensures slseg(res, null);
{
  var curr: Node, prv: Node;
  prv := null;
  curr := lst;
  while (curr != null && curr.data < value) 
    invariant prv == null &*& curr == lst &*& slseg(curr, null) ||
                lslseg(lst, prv, prv.data) &*& prv.next |-> curr &*& slseg(curr, null) &*&
                  (curr == null || prv.data <= curr.data);
  {
    prv := curr;
    curr := curr.next;
  }
  if (curr != null && curr.data == value) {
    if (prv == null) {
      lst := curr.next;
    } else {
      prv.next := curr.next;
    }
    free curr;
  }
  return lst;
}
