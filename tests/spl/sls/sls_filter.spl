include "../include/slslist.spl";

procedure filter (x: Node) 
  returns (res: Node)
  requires slseg(x, null)
  ensures slseg(res, null)
{
  res := x;
  var curr := res;
  var prv: Node := null;
  while (curr != null) 
    invariant (lslseg(res, prv, prv.data) &*& prv.next |-> curr &*& uslseg(curr, null, prv.data))
           || (prv == null &*& res == curr &*& slseg(curr, null))
  {
    var old_curr := curr;
    curr := curr.next;
    var nondet: Bool;
    if (nondet) {
      if (prv != null) {
        prv.next := curr;
      } else {
        res := curr;
      }
      free old_curr;
    } else {
      prv := old_curr;
    }
  }
}
