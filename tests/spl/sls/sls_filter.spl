struct Node {
  var next: Node;
  var data: Int;
}

predicate uslseg(x: Node, y: Node, lb: Int) {
  x == y || x != y &*& acc(x) &*& lb <= x.data &*& uslseg(x.next, y, x.data)
}

predicate slseg(x: Node, y: Node) {
  x == y || x != y &*& acc(x) &*& uslseg(x.next, y, x.data)
}

predicate llseg(x: Node, y: Node, ub: Int) {
  x == y || x != y &*& acc(x) &*& x.data <= ub &*& llseg(x.next, y, ub)
}

predicate lslseg(x: Node, y: Node, ub: Int) {
  slseg(x, y) && llseg(x, y, ub)
}

procedure filter (x: Node) returns (res: Node)
  requires slseg(x, null);
  ensures slseg(res, null);
{
  var curr: Node, prv: Node;
  res := x;
  prv := null;
  curr := res;
  while (curr != null) 
    invariant (lslseg(res, prv, prv.data) &*& prv.next |-> curr &*& uslseg(curr, null, prv.data))
           || (prv == null &*& res == curr &*& slseg(curr, null));
  {
    var old_curr: Node;
    old_curr := curr;
    curr := curr.next;
    var nondet: Bool;
    if (nondet) {
      if (prv != null) {
        prv.next := curr;
      } else {
        res := curr;
      }
      free old_curr;
    } else {
      prv := old_curr;
    }
  }
}
