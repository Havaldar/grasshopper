include "../include/slslist.spl";

procedure merge(a: Node, b: Node) returns (res: Node)
  requires slseg(a, null) &*& slseg(b, null);
  ensures slseg(res, null);
{
  res := null;
  if (a == null) {
    return b;
  } else if (b == null) {
    return a;
  } else if (a.data <= b.data) {
    res := a;
    a := a.next;
  } else {
    res := b;
    b := b.next;
  }
  
  var last: Node;
  last := res;

  while (a != null || b != null)
    invariant (last.next |-> a || last.next |-> b);
    invariant lslseg(res, last, last.data);
    invariant uslseg(a, null, last.data);
    invariant uslseg(b, null, last.data);
  {
    if (a == null || b != null && a.data > b.data) {
      last.next := b;
      last := b;
      b := b.next;
    } else {
      last.next := a;
      last := a;
      a := a.next;
    }
  }
  return res;
}

procedure split(x: Node) returns (y: Node, z: Node)
  requires lseg(x, null);
  ensures lseg(y, null) &*& lseg(z, null);
{
  y, z := x, x;

  var curr: Node;
  curr := x;
  while (curr != null)
    invariant lseg(y, z) &*& lseg(z, curr) &*& lseg(curr, null);
  {
    z := z.next;
    curr := curr.next;
    if (curr != null) {
      curr := curr.next;
    }
  }
  if (z != null) {
    var tmp: Node;
    tmp := z;
    z := z.next;
    tmp.next := null;
  }
}


procedure merge_sort(x: Node) returns (res: Node)
  requires lseg(x, null);
  ensures slseg(res, null);
{
  var x1: Node, x2: Node;
  //x.next := x;
  x1, x2 := split(x);
  var res1: Node;
  res1 := merge_sort(x1);
  var res2: Node;
  res2 := merge_sort(x2);
  return merge(res1, res2);
}
