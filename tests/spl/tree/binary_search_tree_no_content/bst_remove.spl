struct Node {
  var left: Node;
  var right: Node;
  var parent: Node;
  var data: int;
}

predicate bstree(x: Node, y: Node, lb: int, ub: int) {
    x == null
  ||
    x != null &*&
    x.parent |-> y &*&
    lb <= x.data &*&
    x.data <= ub &*&
    bstree(x.left, x, lb, x.data - 1) &*&
    bstree(x.right, x, x.data + 1, ub)
}

predicate tree(x: Node, y: Node) {
    x == null
  ||
    x != null &*&
    x.parent |-> y &*&
    tree(x.left, x) &*&
    tree(x.right, x)
}

predicate treeAllocInvariant(X: set<Node>) {
  (forall n in X :: n.parent == n.parent)
}

procedure extract_max(root: Node, ghost p: Node,
                      ghost lb: int, ghost ub: int)
returns (others: Node, max: Node)
  requires p in Alloc || p == null;
  requires bstree(root, p, lb, ub) &*& root != null;
  requires treeAllocInvariant(Alloc);
  ensures  bstree(others, p, lb, max.data-1) &*& acc(max);
  ensures  max.left == null && max.right == null && max.parent == null && max.data <= ub && lb <= max.data;
  ensures  treeAllocInvariant(Alloc);


procedure remove(root: Node, value: int, p: Node,
                 ghost lb: int, ghost ub: int)
returns (res: Node)
  requires bstree(root, p, lb, ub);
  requires treeAllocInvariant(Alloc);
  ensures bstree(res, p, lb, ub);
  ensures treeAllocInvariant(Alloc);
{
    if (root == null) {
        return null;
    } else if (root.data == value) {
        if (root.left == null && root.right == null) {
            root.parent := null;
            free(root);
            return null;
        } else if (root.left != null && root.right == null) {
            res := root.left;
            root.parent := null;
            free(root);
            res.parent := p;
            return res;
        } else if (root.left == null && root.right != null) {
            res := root.right;
            root.parent := null;
            free(root);
            res.parent := p;
            return res;
        } else {
            var l: Node, m: Node;
            l, m := extract_max(root.left, root, lb, value - 1);
            m.left := l;
            if (l != null) {       
              l.parent := m;
            }
            m.right := root.right;
            m.right.parent := m;
            root.parent := null;
            free(root);
            m.parent := p;
            return m;
        }
    } else if (root.data > value) {
        root.left := remove(root.left, value, root, lb, root.data - 1);
        return root;
    } else {
        root.right := remove(root.right, value, root, root.data + 1, ub);
        return root;
    }
}
