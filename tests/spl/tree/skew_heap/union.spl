include "../../include/bstree.spl";

procedure union(h1: Node, h2: Node,
                implicit ghost content1: Set<Int>,
                implicit ghost content2: Set<Int>)
    returns (res: Node)
    requires heap_set(h1, content1) &*& heap_set(h2, content2);
    ensures heap_set(res, content1 ++ content2);
{
    if (h1 == null) {
        return h2;
    } else if (h2 == null) {
        return h1;
    } else if (h1.data >= h2.data) {
        var u: Node, r: Node;
        r := h1.right;
        h1.right := h1.left;
        if (r != null) { r.parent := null; }
        u := union(h2, r);
        h1.left := u;
        if (u != null) { u.parent := h1; }
        return h1;
    } else {
        return union(h2, h1);
    }
}
