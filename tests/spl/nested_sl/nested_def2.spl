// Structure and predicate declarations for nested lists with head pointers

struct OuterNode {
  var onext: OuterNode;
  var down: InnerNode;
}

struct InnerNode {
  var inext: InnerNode;
  var head: OuterNode;
}

function lseg_footprint(x: InnerNode, y: InnerNode)
  returns (FP: Set<Loc<InnerNode>>)
{
  forall z: Loc<InnerNode> :: z in FP == (Btwn(inext, x, z, y) && z != y && z in InnerNode)
}

predicate lseg(x: InnerNode, y: InnerNode, FP: Set<Loc<InnerNode>>) {
  Reach(inext, x, y) && 
  FP == lseg_footprint(x, y)
}

function nlseg_outer_footprint(x: OuterNode, y: OuterNode) 
  returns (FP: Set<Loc<OuterNode>>) 
{
  forall z: Loc<OuterNode> :: z in FP == (Btwn(onext, x, z, y) && z != y && z in OuterNode)
}

function dinext() returns(m: Map<Loc<OuterNode>, Loc<InnerNode>>) {
  forall x: Loc<OuterNode>, y: Loc<InnerNode> :: x.m == y == (x.down == y || x.inext == y)
    @(matching x.down yields m[x])
    @(matching x.inext yields m[x])
}

function nlseg_inner_footprint(x: OuterNode, y: OuterNode) 
  returns (FP: Set<Loc<InnerNode>>)
{
  (forall z: Loc<InnerNode> :: z in FP ==>
   z.head in nlseg_outer_footprint(x, y)) &&
  (forall z: Loc<InnerNode>, zh: Loc<OuterNode> :: z in FP && zh == z.head ==>
   /* z in lseg_footprint(zh.down, null) */
   Btwn(inext, zh.down, z, null) && z != null
   @(matching z yields z.head)
   @(matching zh yields zh.down)) &&
  (forall zh: Loc<OuterNode>, z: Loc<InnerNode> :: 
   zh in nlseg_outer_footprint(x, y) &&
   /*z in lseg_footprint(zh.down, null)*/
   Btwn(inext, zh.down, z, null) && z != null ==> z in FP)
}

predicate nlseg(x: OuterNode, y: OuterNode, FP0: Set<Loc<OuterNode>>, FP1: Set<Loc<InnerNode>>) {
  Reach(onext, x, y) &&
  (forall u: OuterNode, v: InnerNode ::
     u in nlseg_outer_footprint(x, y) && v in nlseg_inner_footprint(x, y) && 
     Reach(inext, u.down, v) ==> v.head == u) &&
  (forall u: OuterNode :: u in nlseg_outer_footprint(x, y) ==> Reach(inext, u.down, null)) &&
  FP0 == nlseg_outer_footprint(x, y) &&
  FP1 == nlseg_inner_footprint(x, y)
}
