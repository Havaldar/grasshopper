include "../include/dllist.spl";

procedure dl_remove(a: Node, b: Node) returns (c: Node, d: Node)
  requires dlseg(a, null, null, b);
  ensures dlseg(c, null, null, d);
{
  if (a == null) {
    return a, b;   
  } else if (a == b) {
    free a;
    return null, null;
  } else {
    var curr: Node;
    c, d := a, b;
    curr := c;
    
    var nondet: Bool;
    while (nondet && curr.next != null) 
      invariant acc(curr) -** dlseg(c, null, null, d);
    {
      curr := curr.next;
      havoc nondet;
    }
    
    var nxt: Node;
    nxt := curr.next;
    var prv: Node;
    prv := curr.prev;
    if (prv != null) {
      prv.next := nxt;
    } else {
      c := nxt;
    }
    if (nxt != null) {
      nxt.prev := prv;
    } else {
      d := prv;
    }
    free curr;
    return c, d;
  }
}
