include "../include/dllist.spl";

// Implementation of Dancing Links (see Problem 3 of Verify This Competition 2015)

procedure remove(l: Node, x: Node, ghost e: Node, implicit ghost FP: Set<Node>)
  requires (acc(x.prev) &*& acc(x) &*& acc(x.next)) -** dlseg(l, null, null, e, FP)
  ensures ((acc(x.prev) &*& acc(x.next)) -** dlseg(l, null, null, e, FP -- Set(x))) &*& acc(x)
  ensures x.next.prev == x.prev
  ensures x.next == x.prev.next
  ensures forall z1 in FP -- Set(x), z2 in FP -- Set(x) :: Reach(old(next), z1, z2) ==> Reach(next, z1, z2)
{
  var xn := x.next;
  xn.prev := x.prev;
  var xp := x.prev;
  xp.next := x.next;
}


procedure readd(l: Node, x: Node, ghost e: Node)
  requires ((acc(x.prev) &*& acc(x.next)) -** dlseg(l, null, null, e)) &*& acc(x)
  requires x.next.prev == x.prev
  requires x.next == x.prev.next
  ensures (acc(x.prev) &*& acc(x) &*& acc(x.next)) -** dlseg(l, null, null, e)
{
  var xn := x.next;
  xn.prev := x;
  var xp := x.prev;
  xp.next := x;
}
