struct Node {
  var next: Node;
  var data: int;
}

predicate uslseg(x: Node, y: Node, lb: int) {
  x == y || x != y &*& x &*& lb <= x.data &*& uslseg(x.next, y, x.data)
}

predicate slseg(x: Node, y: Node) {
  x == y || x != y &*& x &*& uslseg(x.next, y, x.data)
}

predicate llseg(x: Node, y: Node, ub: int) {
  x == y || x != y &*& x &*& x.data <= ub &*& llseg(x.next, y, ub)
}

predicate lslseg(x: Node, y: Node, ub: int) {
  slseg(x, y) && llseg(x, y, ub)
}


procedure sls_pairwise_sum(x: Node, y: Node) returns (z: Node)
  requires slseg(x, null) &*& slseg(y, null);
  ensures slseg(x, null) &*& slseg(y, null) &*& slseg(z, null);
{
  if (x == null || y == null) {
    return null;
  }
  var curr_x: Node, curr_y: Node, last_z: Node;
  curr_x := x;
  curr_y := y;
  z := new Node;
  last_z := z;
  z.data := curr_x.data + curr_y.data;
  z.next := null;
  while (curr_x.next != null && curr_y.next != null) 
    invariant curr_x != null &*& lslseg(x, curr_x, curr_x.data) &*& slseg(curr_x, null);
    invariant curr_y != null &*& lslseg(y, curr_y, curr_y.data) &*& slseg(curr_y, null);
    invariant lslseg(z, last_z, last_z.data) &*& last_z.next |-> null;
    //invariant last_z.data <= curr_x.data + curr_y.data;
  {
    var tmp: Node;
    tmp := last_z;
    curr_x := curr_x.next;
    curr_y := curr_y.next;
    last_z := new Node;
    last_z.next := null;
    last_z.data := curr_x.data + curr_y.data;        
    tmp.next := last_z;
  }
  return z;
}
