//  inspired by
//  http://opendatastructures.org/ods-java/14_2_B_Trees.html
//  under CC-BY
//  full source at http://opendatastructures.org/ods-java.tgz

struct T { }

predicate lt(x: T, y: T) {
  lt(x, y) 
}

predicate le(x: T, y: T) {
  le(x, y)
}

axiom forall a: T :: !lt(a, a);
axiom forall a: T, b: T :: !lt(a, b) || !lt(b, a);
axiom forall a: T, b: T :: lt(a, b) || lt(b, a) || a == b;
axiom forall a: T, b: T, c: T :: lt(a, b) && lt(b, c) ==> lt(a, c);
axiom forall a: T, b: T :: le(a, b) == (lt(a, b) || a == b);

function compare(x: T, y: T)
  returns (res: Int)
  ensures (x == y) == (res == 0)
  ensures lt(x, y) == (res < 0)
  ensures lt(y, x) == (res > 0)

/* The order, B, is at least 2 */
var B: Int;
axiom B >= 2;

struct Node {
  var keys: Array<T>;
  var children: Array<Node>;

  /* NOTE: adding these */
  var ghost parent: Node;
  var ghost is_leaf: Bool;
  var ghost lbound: T;
  var ghost ubound: T;
}

/* -------------------- Predicate Definitions -------------------- */

function key_arrayseg_fp(a: Array<T>, i: Int, j: Int)
  returns (FP: Set<ArrayCell<T>>)
  ensures forall c: ArrayCell<T> :: c in FP == (i <= c.index && c.index < j && c.array == a)

predicate key_arrayseg(a: Array<T>, i: Int, j: Int) {
  0 <= i && i <= j && j <= a.length &*&
  acc(key_arrayseg_fp(a, i, j))
}

predicate sorted_key_arrayseg(a: Array<T>, i: Int, j: Int) {
  acc(key_arrayseg_fp(a, i, j)) &*&
  0 <= i && i <= j && j <= a.length &&
  (forall k: Int, l: Int:: k >= i && k <= l && l < j ==> lt(a[k], a[l]))
}

function node_arrayseg_fp(a: Array<Node>, i: Int, j: Int)
  returns (FP: Set<ArrayCell<Node>>)
  ensures forall c: ArrayCell<Node> :: c in FP == (i <= c.index && c.index < j && c.array == a)

predicate node_arrayseg(a: Array<Node>, i: Int, j: Int) {
  0 <= i && i <= j && j <= a.length &*&
  acc(node_arrayseg_fp(a, i, j))
}

function node_footprint(r: Node)
  returns (FP: Set<Node>)
  ensures forall x: Node :: x in FP == (Btwn(parent, x, r, r) && r != null)

function key_array_owner(a: Array<T>, FP_N: Set<Node>) returns (n: Node)

function array_int_fp(FP_N: Set<Node>)
  returns (FP_A_I: Set<Array<T>>)
  ensures forall n: Node :: n in FP_N ==> n.keys in FP_A_I
            @(matching n yields n.keys)
  ensures forall a: Array<T> :: a in FP_A_I ==> key_array_owner(a, FP_N) in FP_N && key_array_owner(a, FP_N).keys == a
            @(matching a without array_node_owner yields key_array_owner(a, FP_N).keys)

function array_node_owner(a: Array<Node>, FP_N: Set<Node>)
  returns (n: Node)

function array_node_fp(FP_N: Set<Node>)
  returns (FP_A_N: Set<Array<Node>>)
  ensures forall n: Node :: n in FP_N ==> n.children in FP_A_N
            @(matching n yields n.children)
  ensures forall a: Array<Node> :: a in FP_A_N ==> array_node_owner(a, FP_N) in FP_N && array_node_owner(a, FP_N).children == a
            @(matching a without key_array_owner yields array_node_owner(a, FP_N).children)

function arraycell_int_fp(FP_A_I: Set<Array<T>>)
  returns (FP_AC_I: Set<ArrayCell<T>>)
  ensures forall a: Array<T>, ac: ArrayCell<T> :: a in FP_A_I && ac in key_arrayseg_fp(a, 0, a.length) ==> ac in FP_AC_I
            @(matching a yields key_arrayseg_fp(a, 0, a.length))
  ensures forall ac: ArrayCell<T> :: ac in FP_AC_I ==> ac.array in FP_A_I

function arraycell_node_fp(FP_A_N: Set<Array<Node>>)
  returns (FP_AC_N: Set<ArrayCell<Node>>)
  ensures forall a: Array<Node>, ac: ArrayCell<Node> :: a in FP_A_N && ac in node_arrayseg_fp(a, 0, a.length) ==> ac in FP_AC_N
            @(matching a yields node_arrayseg_fp(a, 0, a.length))
  ensures forall ac: ArrayCell<Node> :: ac in FP_AC_N ==> ac.array in FP_A_N

predicate btree(b: Node) {
  exists FP_N: Set<Node>, FP_A_I: Set<Array<T>>, FP_AC_I: Set<ArrayCell<T>>, FP_A_N: Set<Array<Node>>, FP_AC_N: Set<ArrayCell<Node>> ::
    acc(FP_N) &*& acc(FP_A_I) &*& acc(FP_AC_I) &*& acc(FP_A_N) &*& acc(FP_AC_N) &*&
    FP_N == node_footprint(b) &&
    FP_A_I == array_int_fp(FP_N) &&
    FP_AC_I == arraycell_int_fp(FP_A_I) &&
    FP_A_N == array_node_fp(FP_N) &&
    FP_AC_N == arraycell_node_fp(FP_A_N) &&
 
    /* keys and children arrays are of size 2B */
    (forall n: Node :: n in FP_N ==> n.children.length == 2 * B && n.keys.length == 2 * B) &&
  
    /* Structural properties of arrays */
    // keys arrays are partitioned into non-null/null halfs
    (forall a in FP_A_I, i: Int, j: Int :: 0 <= i && i <= j && j < a.length && a[i] == null ==> a[j] == null) &&
    // children arrays are partitioned into non-null/null halfs
    (forall a in FP_A_N, i: Int, j: Int :: 0 <= i && i <= j && j < a.length && a[i] == null ==> a[j] == null) &&
    // if a child entry is null then, so is the corresponding key entry
    (forall n in FP_N, i: Int :: 0 <= i && i < n.keys.length && n.children[i] == null ==> n.keys[i] == null) &&
    // if a key entry is null, then it is either the first null entry in the key array
    // or the corresponding child entry is also null
    (forall n in FP_N, i: Int, j: Int :: 0 <= i && i < j && j < n.keys.length && n.keys[i] == null && n.keys[j] == null ==> n.children[j] == null) && 
    // the keys are sorted
    (forall a in FP_A_I, i: Int, j: Int :: 0 <= i && i <= j && j < a.length ==> a[i] == null || a[j] == null || lt(a[i], a[j])) &&
    
    /* Tree invariants */
    // parent is inverse of child array 
    (forall c in FP_AC_N :: c[] == null || Reach(parent, c[], array_node_owner(c.array, FP_N)) && c[] != array_node_owner(c.array, FP_N)) &&
    (forall c in FP_AC_N, x: Node :: Btwn(parent, c[], x, array_node_owner(c.array, FP_N)) ==> x == array_node_owner(c.array, FP_N) || c[] == x) &&
    // child array entries are all distinct
    (forall c in FP_AC_N, d in FP_AC_N :: c[] == d[] ==> c == d || c[] == null) &&
    // ancestor nodes must be reachable via correct child
    (forall x in FP_N, c in FP_AC_N :: Reach(parent, x, array_node_owner(c.array, FP_N)) && le(c[].lbound, x.lbound) && le(x.ubound, c[].ubound) ==>
     Btwn(parent, x, c[], array_node_owner(c.array, FP_N)) @(matching x yields x.lbound) @(matching x yields x.ubound)) &&
    // keys of ancestor nodes include keys of decendants
    (forall x in FP_N, y in FP_N :: Reach(parent, x, y) ==> le(y.lbound, x.lbound) && le(x.ubound, y.ubound)) &&
    // keys of children are disjoint
    (forall c in FP_AC_N, d in FP_AC_N :: c.index < d.index && c.array == d.array ==> lt(c[].ubound, d[].lbound)) &&
    // key bounds of nodes are consistent
    (forall x in FP_N :: le(x.lbound, x.ubound)) &&
    // ...TODO
    
    true
}

/* -------------------- Actual Implementation -------------------- */

/* Find the smallest value >= x in this BTree */
procedure find(b: Node, x: T) returns (res: T)
  requires btree(b) &*& b != null
  ensures btree(b)
{
  var z: T := null;
  var n := b;
  var flag := false;
  while (n != null && !flag)
    invariant (n == null || acc(n)) -** btree(b)
  {
    var i := findIt(n.keys, x);
    if (n.keys[i] == x) {
      z := n.keys[i];
      flag := true;  /* Found x, so return */
    } else {
      /* n.keys[i] is smallest value > x, go to child[i] if exists */
      if (n.keys != null) {
        z := n.keys[i];
      }
      n := n.children[i];
    }
  }
  return z;
}

procedure findIt(a: Array<T>, x: T) returns (idx: Int)
  requires key_arrayseg(a, 0, a.length)
  ensures key_arrayseg(a, 0, a.length)
  ensures 0 <= idx && idx < a.length
{ }
/* TODO for nomodifiesopt, better to give body for proc or not? */

/* For simplicity, replaced this with array/bsearch.spl
procedure findIt(a: Array<Int>, x: Int, begin: Int, end: Int) returns (idx: Int)
  requires int_arrayseg(a, begin, end)
  requires forall i: Int, j: Int :: begin <= i && i < j && j < end ==> a[i] <= a[j]
  requires 0 <= begin && begin < end && end <= a.length
  ensures int_arrayseg(a, begin, end)
  ensures begin <= idx && (idx < end && a[idx] >= x && (idx > begin ==> a[idx-1] < x)) || (idx == end && a[idx-1] < x)
{
  var lo := begin;
  var hi := end;

  while (hi > lo)
    invariant int_arrayseg(a, begin, end)
    invariant forall i: Int, j: Int :: begin <= i && i < j && j < end ==> a[i] <= a[j]
    invariant begin <= lo && lo <= hi && hi <= end
    invariant (lo < end && (a[lo] < x || (a[lo] >= x && (lo > begin ==> a[lo-1] < x)))) || (lo == end && a[lo-1] < x)
    invariant hi == end || a[hi] >= x
  {
    var mid := (lo + hi) / 2;
    if (a[mid] < x)
    {
      lo := mid + 1;
    }
    else
    {
      hi := mid;
    }
  }
  return lo;
}
*/
