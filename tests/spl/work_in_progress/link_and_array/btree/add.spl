include "def.spl";
include "find.spl";
include "node_utils.spl";
include "array_utils.spl";

procedure splitNode(leftNode: Node) returns (splitValue: T, rightNode: Node)
  requires  btree(leftNode) &*& leftNode != null
  requires  leftNode.keys[b-1] != bottom // node is full
  ensures   btree(leftNode) &*& btree(rightNode)
  ensures   splitValue == old(leftNode.keys[b/2])
  // state of the left node
  ensures   leftNode != null && leftNode.parent == old(leftNode.parent)
  ensures   leftNode.lbound == old(leftNode.lbound) && leftNode.ubound == splitValue
  ensures   forall i: Int :: 0 <= i && i < b/2 ==> leftNode.keys[i] == old(leftNode.keys[i])
  ensures   forall i: Int :: 0 <= i && i <= b/2 ==> leftNode.children[i] == old(leftNode.children[i])
  ensures   forall i: Int :: b/2 <= i && i < b ==> leftNode.keys[i] == bottom
  ensures   forall i: Int :: b/2 < i && i < b ==> leftNode.children[i] == null
  // state of the rightNode node
  ensures   rightNode != null && rightNode.parent == null
  ensures   rightNode.ubound == old(leftNode.ubound) && rightNode.lbound == rightNode.keys[0]
  ensures   forall i: Int :: 0 <= i && i < b/2 ==> rightNode.keys[i] == old(leftNode.keys[i + b/2])
  ensures   forall i: Int :: 0 <= i && i <= b/2 ==> rightNode.children[i] == old(leftNode.children[i + b/2])
  ensures   forall i: Int :: b/2 <= i && i < b ==> rightNode.keys[i] == bottom
  ensures   forall i: Int :: b/2 < i && i < b ==> rightNode.children[i] == null
{
  rightNode := newNode();
  var j := b/2;
  // move the keys
  key_arraycopy(leftNode.keys, j+1, rightNode.keys, 0, b-j-1);
  key_fill(leftNode.keys, j+1, b, bottom);
  // move the children
  node_arraycopy(leftNode.children, j+1, rightNode.children, 0, b-j);
  node_fill(leftNode.children, j+1, b+1, null);
  // extract the middle value
  splitValue := leftNode.keys[j];
  leftNode.keys[j] := bottom;
}

procedure addRecursive(x: T, u: Node) returns (res: Node)
  requires  btree(u) &*& u != null
  ensures   btree(u) &*& btree(res)
{
  var i := findIt(u.keys, x);
  if (i < u.keys.length && u.keys[i] == x) {
    //throw new DuplicateValueException();
    return null;
  }
  if (is_leaf(u)) { // leaf node, just add it
    var added := addNode(u, x, null);
    pure assert(added); // XXX
  } else {
    var w := addRecursive(x, u.children[i]);
    if (w != null) {  // child was split, w is new child 
      x := removeNode(w, 0);
      var added := addNode(u, x, w);
      pure assert(added); // XXX
    }
  }
  if (is_full(u)) {
    var s: T, n: Node;
    s, n := splitNode(u);
    // XXX what about s
    return n;
  } else {
    return null;
  }
}

procedure add(bt: Node, x: T) returns (res: Node)
  requires  btree(bt) &*& bt != null
  ensures   btree(res)
{
  var w: Node;
  w := addRecursive(x, bt);
  if (w != null) {   // root was split, make new root
    var newroot := newNode();
    x := removeNode(w, 0);
    newroot.children[0] := bt;
    bt.parent := newroot;
    newroot.keys[0] := x;
    newroot.children[1] := w;
    w.parent := newroot;
    return newroot;
  } else {
    return bt;
  }
}
