include "def.spl";

/* Find the smallest value >= x in this BTree */
procedure find(bt: Node, x: T) returns (res: T)
  requires x != bottom
  requires acc(bt) -** btree(bt)
  ensures btree(bt)
{
  var z: T := bottom;
  var n := bt;
  var flag := false;
  while (n != null && !flag)
    invariant (n == null || acc(n)) -** btree(bt)
  {
    var i := findIt(n.keys, x);
    if (i < n.keys.length && n.keys[i] == x) {
      z := n.keys[i];
      flag := true;  /* Found x, so return */
    } else {
      /* n.keys[i] is smallest value > x, go to child[i] if exists */
      if (i < n.keys.length && n.keys[i] != bottom) {
        z := n.keys[i];
      }
      n := n.children[i];
    }
  }
  return z;
}

procedure findIt(a: Array<T>, x: T) returns (idx: Int)
  requires a.length == b
  requires acc(a)
  requires key_arrayseg(a, 0, a.length)
  requires forall i: Int, j: Int :: 0 <= i && i < j && j < a.length ==> a[i] == bottom || a[j] == bottom || lt(a[i], a[j])
  requires forall i: Int, j: Int :: 0 <= i && i <= j && j < a.length && a[i] == bottom ==> a[j] == bottom
  ensures a.length == b
  ensures acc(a)
  ensures key_arrayseg(a, 0, a.length)
  ensures forall i: Int, j: Int :: 0 <= i && i < j && j < a.length ==> a[i] == bottom || a[j] == bottom || lt(a[i], a[j])
  ensures forall i: Int, j: Int :: 0 <= i && i <= j && j < a.length && a[i] == bottom ==> a[j] == bottom
  // what we actually care about
  ensures 0 <= idx && idx <= a.length
  ensures idx == a.length || a[idx] == x || a[idx] == bottom || lt(x, a[idx])
  ensures forall i: Int :: 0 <= i && i < idx ==> a[i] != bottom && lt(a[i], x)
  ensures forall i: Int :: idx < i && i < a.length ==> a[i] == bottom || lt(x, a[i])
{
  var lo := 0;
  var hi := a.length;

  while (hi != lo)
    invariant a.length > 0
    invariant acc(a)
    invariant key_arrayseg(a, 0, a.length)
    invariant forall i: Int, j: Int :: 0 <= i && i < j && j < a.length ==> a[i] == bottom || a[j] == bottom || lt(a[i], a[j])
    invariant forall i: Int, j: Int :: 0 <= i && i <= j && j < a.length && a[i] == bottom ==> a[j] == bottom
    // what we actually care about
    invariant 0 <= lo && lo <= hi && hi <= a.length
    invariant forall i: Int :: 0 <= i && i < lo ==> a[i] != bottom && lt(a[i], x)
    invariant forall i: Int :: hi < i && i < a.length ==> a[i] == bottom || lt(x, a[i])
  {
    var m := (hi + lo) / 2;
    var cmp: Int;
    if (a[m] == bottom) {
      cmp := -1;
    } else {
      cmp := compare(x, a[m]);
    }
    if (cmp < 0) {
      hi := m;      // look in first half
    } else if (cmp > 0) {
      lo := m+1;    // look in second half
    } else {
      // found it
      hi := m;
      lo := m;
    }
  }
  return lo;
}
