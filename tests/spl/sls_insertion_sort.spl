struct Node {
  var next: Node;
  var data: int;
}

predicate uslseg(x: Node, y: Node, lb: int) {
  x == y || x != y &*& acc(x) &*& lb <= x.data &*& uslseg(x.next, y, x.data)
}

predicate ulseg(x: Node, y: Node, lb: int) {
  x == y || x != y &*& acc(x) &*& lb <= x.data &*& ulseg(x.next, y, lb)
}

predicate llseg(x: Node, y: Node, ub: int) {
  x == y || x != y &*& acc(x) &*& x.data <= ub &*& llseg(x.next, y, ub)
}

predicate slseg(x: Node, y: Node) {
  x == y || x != y &*& acc(x) &*& uslseg(x.next, y, x.data)
}

predicate lseg(x: Node, y: Node) {
  x == y || x != y &*& acc(x) &*& lseg(x.next, y)
}

predicate lslseg(x: Node, y: Node, ub: int) {
  slseg(x, y) && llseg(x, y, ub)
}

procedure insertion_sort(lst: Node) 
  requires lseg(lst, null);
  ensures slseg(lst, null);
{
  var prv: Node, srt: Node;
  prv := null;
  srt := lst;
  while (srt != null)
    invariant (prv == null &*& srt == lst &*& lseg(lst, null))
           || (lslseg(lst, prv, prv.data) &*& prv.next |-> srt &*& ulseg(srt, null, prv.data));
  {
    var curr: Node, min: Node;
    curr := srt.next;
    min := srt;
    while (curr != null)
      invariant prv == null &*& lseg(curr, null)
             || ulseg(curr, null, prv.data) &*& prv.data <= min.data;
      invariant ulseg(srt, min, min.data);
      invariant ulseg(min, curr, min.data);
      invariant min != null;
    {
      if (curr.data < min.data) {
        min := curr;
      }
      curr := curr.next;
    }
    var tmp: int;
    tmp := min.data;
    min.data := srt.data;
    srt.data := tmp;
    prv := srt;
    srt := srt.next;
  }
}
