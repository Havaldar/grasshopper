include "insert.spl";  // Has definitions of list set with uninterpreted Data
include "../include/node_array.spl";

struct HashSet {
  var size: Int; /* the size of the set */
  var buckets: Array<Node>; /* the set elements */
}

function arrayseg_buckets_footprint(a: Array<Node>, FP0: Set<ArrayCell<Node>>)
  returns (FP1: Set<Node>)
  requires acc(FP0)
  ensures // all nodes reachable from the head of the bucket are in the footprint
    (forall c: ArrayCell<Node>, x: Node :: c in FP0 && Btwn(next, c.array[c.index], x, null) ==> x in FP1 || x == null)
  ensures // all nodes in the footprint are reachable from the bucket head
    (forall x in FP1 :: Btwn(next, a[hash(a.length, x.data)], x, null) && x != null @(matching x yields a[hash(a.length, x.data)]))
  ensures // the bucket heads are in the array's footprint
    (forall x in FP1 :: a.cells[hash(a.length, x.data)] in FP0 @(matching x yields a.cells[hash(a.length, x.data)] ))

predicate arrayseg_with_buckets(a: Array<Node>, lo: Int, hi: Int, cnt: Set<Data>) {
  exists FP0: Set<ArrayCell<Node>>, FP1: Set<Node> ::
    arrayseg(a, lo, hi) && acc(FP0) &*&
    FP1 == arrayseg_buckets_footprint(a, FP0) &*& acc(FP1) &*&
    (forall c: ArrayCell<Node>, x in FP1 :: c in FP0 && Reach(next, c.array[c.index], x) ==> hash(a.length, x.data) == c.index) &&
    //(forall c: ArrayCell<Node>, x in FP1 :: c in FP0 && Reach(next, c.array[c.index], x) ==> a.cells[hash(a.length, x.data)] == c) &&
    (forall c: ArrayCell<Node> :: c in FP0 ==> Reach(next, c.array[c.index], null)) &&
    //all the elts from a list have the same hash, and this is the index in the array
    (forall v in FP1 :: Btwn(next, a[hash(a.length, v.data)], v, null) @(matching v yields a[hash(a.length, v.data)])) &&
    //the buckets are strictly sorted
    (forall y in FP1, z in FP1 :: Btwn(next,y,z,z) ==> lt(y.data, z.data) || y == z) &&
    cnt == content(FP1)
}

predicate array_with_buckets(a: Array<Node>, cnt: Set<Data>) {
  acc(a) &*&
  arrayseg_with_buckets(a, 0, a.length, cnt)
}


//some ghost function that we can use for the spec of hash
function hash(max: Int, value: Data) returns (res: Int)
  requires max > 0
  ensures res >= 0 && res < max

predicate hset(h: HashSet, cnt: Set<Data>) {
  acc(h) &*&
  array_with_buckets(h.buckets, cnt) &*&
  h.size == h.buckets.length &&
  h.size > 0
}

procedure create(_size: Int)
  returns (res: HashSet)
  requires 0 < _size
  ensures hset(res, {})
{
  var new_set := new HashSet;
  new_set.buckets := new Array<Node>(_size);    
  var i := 0;
  while (i < _size)
    invariant 0 <= i && i <= _size && new_set.buckets.length == _size
    invariant acc(new_set) &*& acc(new_set.buckets)
    invariant arrayseg_with_buckets(new_set.buckets, 0, i, {})
    invariant arrayseg(new_set.buckets, i, _size)
  {
    new_set.buckets[i] := null;
    i := i + 1;
  }
  new_set.size := _size;

  return new_set;
}


procedure add(hashset: HashSet, value: Data, implicit ghost content: Set<Data>)
  requires hset(hashset, content)
  ensures hset(hashset, content ++ {value})
{  
  var hashval := hash(hashset.size, value);
  var list := hashset.buckets[hashval];
  hashset.buckets[hashval] := insert(list, value);
}

