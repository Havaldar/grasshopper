struct Node {
  var next: Node;
  var prev: Node;
}

predicate dlseg(x: Node, u: Node, y: Node, v: Node) {
  x == y && u == v || 
  x != y &*& x.prev == u &*& acc(x) &*& dlseg(x.next, x, y, v)
}

procedure dl_traverse(lst: Node, ghost a: Node, ghost b: Node)
  requires dlseg(lst, a, null, b);
  ensures dlseg(lst, a, null, b);
{
  var curr: Node, prv: Node;
  curr := lst;
  prv := a;
  while (curr != null) 
    invariant dlseg(lst, a, curr, prv) &*& dlseg(curr, prv, null, b);
  {
    prv := curr;
    curr := curr.next; 
  }
}

procedure dl_traverse2(lst: Node, tsl: Node)
  requires dlseg(lst, null, null, tsl);
  ensures dlseg(lst, null, null, tsl);
{
  var curr: Node, nxt: Node;
  curr := tsl;
  nxt := null;
  while (curr != null) 
    invariant dlseg(lst, null, nxt, curr);
    invariant dlseg(nxt, curr, null, tsl);
  {
    nxt := curr;
    curr := curr.prev; 
  }
}

