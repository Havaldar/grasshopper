method merge(a, b)
    requires slseg(next, data, a, null) * slseg(next, data, b, null);
    ensures slseg(next, data, returned, null);
{
    head := null;
    if (a == null) {
        return b;
    } else if (b == null) {
        return a;
    } else if (a.data <= b.data) {
        head := a;
        a := a.next;
    } else {
        head := b;
        b := b.next;
    }
    last := head;
    while (a != null || b != null)
        invariant (last |-> a || last |-> b) * lslseg(next, data, head, last, last.data) * uslseg(next, data, a, null, last.data) * uslseg(next, data, b, null, last.data);
    {
        if (a == null || (b != null && a.data > b.data)) {
            last.next := b;
            last := b;
            b := b.next;
        } else {
            last.next := a;
            last := a;
            a := a.next;
        }
    }
    return head;
}

method length(x)
    requires lseg(next, x, null);
    ensures lseg(next, x, null);
{
    i := 0;
    curr := x;
    while (curr != null)
        invariant lseg(next, x,curr) * lseg(next, curr, null);
    {
        curr := curr.next;
        i := i+1;
    }
    return i;
}

method take(x, n)
    requires lseg(next, x, null);
    ensures lseg(next, x, null) * lseg(next, returned, null);
{
    y := x;
    while (y != null && n > 1)
        invariant lseg(next, x, y) * lseg(next, y, null);
    {
        y := y.next;
        n := n-1;
    }
    if (y != null) {
        tmp := y;
        y := y.next;
        tmp.next := null;
    }
    return y;
}

method merge_sort(lst)
    requires lseg(next, lst, null);
    ensures slseg(next, data, returned, null);
{
    if (lst == null || lst.next == null) {
        return lst;
    } else {
        n := length(lst);
        n2 := n / 2;
        lst2 := take(lst, n2);
        //assert lseg(next, lst, null) * lseg(next, lst2, null);
        a := merge_sort(lst);
        //assert slseg(next, data, a, null) * lseg(next, lst2, null);
        b := merge_sort(lst2);
        //assert slseg(next, data, a, null) * slseg(next, data, b, null);
        merged := merge(a, b);
        return merged;
    }
}
