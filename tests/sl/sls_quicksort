
method predecessor(start, end, lb, ub)
    requires blseg(next, data, start, end, lb, ub) * start != end;
    ensures blseg(next, data, start, returned, lb, ub) * returned |-> end * returned.data >= lb * returned.data <= ub;
{
    prv := start;
    curr := start.next;
    while (curr != end)
        invariant blseg(next, data, start, prv, lb, ub) * blseg(next, data, curr, end, lb, ub) * prv |-> curr * prv.data >= lb * prv.data <= ub;
    {
        prv := curr;
        curr := curr.next;
    }
    return prv;
}

method split(lst, tsl, lb, ub)
    requires blseg(next, data, lst, tsl, lb, ub) * lst != tsl;
    ensures blseg(next, data, lst, returned, lb, returned.data) * blseg(next, data, returned, tsl, returned.data, ub) * returned != tsl;
{
    pivot := predecessor(lst, tsl, lb, ub);
    if (lst == pivot) {
        return lst;
    } else {
        last := predecessor(lst, pivot, lb, ub);
        start := lst;
        end := last;
        while (start != end)
            invariant blseg(next, data, lst, start, lb, pivot.data);
            invariant blseg(next, data, start, end, lb, ub);
            invariant blseg(next, data, end, last, pivot.data, ub);
            invariant last |-> pivot;
            invariant last.data >= lb;
            invariant last.data <= ub;
            invariant pivot |-> tsl; 
            invariant pivot.data >= lb;
            invariant pivot.data <= ub;
        {
            tmp := 0; //for the typer ...
            if (start.data <= pivot.data) {
                start := start.next;
            } else if (end.data > pivot.data) {
                end := predecessor(start, end, lb, ub);
            } else {
                tmp := start.data;
                start.data := end.data;
                end.data := tmp;
            }
        }
        if (start == last) {
            return lst;
        } else if (start == lst) {
            return pivot;
        } else {
            last.next := pivot.next;
            pivot.next := start.next;
            start.next := pivot;
            return lst;
        }
    }
}

method quicksort(lst, end, lb, ub)
    requires blseg(next, data, lst, end, lb, ub);
    ensures bslseg(next, data, lst, end, lb, ub);
{
    if (lst == end || lst.next == end) {
    } else {
        middle := split(lst, end, lb, ub);
        quicksort(lst, middle, lb, middle.data);
        quicksort(middle, end, middle.data, ub);
    }
}
